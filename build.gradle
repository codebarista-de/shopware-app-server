plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.vanniktech.maven.publish' version '0.34.0'
    id 'org.springframework.boot' version '3.5.11'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'de.codebarista'
version = '1.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.11'
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-webflux'
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'com.aventrix.jnanoid:jnanoid:2.0.0'

    api 'org.liquibase:liquibase-core:4.29.0'
    api 'org.xerial:sqlite-jdbc:3.45.2.0'
    api 'org.hibernate.orm:hibernate-community-dialects:6.4.4.Final'
    api 'org.openapitools:jackson-databind-nullable:0.2.6'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

// Remove commons-logging from all dependencies
configurations.api {
    exclude group: 'commons-logging', module: 'commons-logging'
}

// Disable Spring Boot's bootJar task for library projects
jar {
    enabled = true
    archiveClassifier = ''
}

bootJar {
    enabled = false
}

mavenPublishing {
    pom {
        name.set("Shopware App Server")
        description.set("Java Spring Boot library for building Shopware 6 App backends")
        url.set("https://github.com/codebarista-de/shopware-app-server")

        licenses {
            license {
                name.set("MIT License")
                url.set("https://opensource.org/licenses/MIT")
            }
        }

        developers {
            developer {
                id.set("codebarista.de")
                name.set("Codebarista")
            }
        }

        scm {
            connection.set("scm:git:git://github.com/codebarista-de/shopware-app-server.git")
            developerConnection.set("scm:git:ssh://github.com/codebarista-de/shopware-app-server.git")
            url.set("https://github.com/codebarista-de/shopware-app-server")
        }
    }

    publishToMavenCentral()

    // Sign when in-memory signing key is provided (CI) or local signing.keyId exists
    if (project.hasProperty("signingInMemoryKeyId") || project.hasProperty("signing.keyId")) {
        signAllPublications()
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}
